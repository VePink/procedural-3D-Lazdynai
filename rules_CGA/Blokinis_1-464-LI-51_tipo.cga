version "2016.0"

/**
 * File:    Sovietiniai_daugiabuciai.cga
 * Created: 26 Nov 2017 09:12:02 GMT
 * Author:  Ve
 */

	const yellow 	= "#FFFF00"
	const red 		= "#ff0000"
	const blue 		= "#002bff"
	const green 	= "#06b400"
	const violet	= "#851bc1"
	const grey		= "#9a9999"

#__________ASSETS__________________#
attr AzuroBlokas	= "Azurine_sekcija/Azurine_sekcija5.dae"

	
#---------- ATTRIBUTES ------------#
@Group ("Orientavimas", 1) //{
	attr Priekinio_fasado_plokstumos_nr = 3
	attr Galinio_fasado_plokstumos_nr 	= 5
//}

@Group ("Kita")//{
	attr H_pastato 					= 25
	attr H_virslangio 				= 0.5
	attr H_lango 					= 1.5
	attr H_polangio 				= 1.2
	attr Plotis_lango 				= 1.6
	attr H_parapeto 				= 0.3
	attr plotis_parapeto 			= 0.3
	attr objektu_ant_stogo_skaicius = 7
	attr H_pamato 					= 1
	attr H_pirmo_auksto 			= 2.8
	attr H_perdangos 				= 0.1
	attr H_vidurinio_auksto 		= 2.8
	attr H_pastoges					= 0.5
	attr laiptines_fasado_plotis 	= 3
	attr balkono_plotis 			= 2.5
	
	attr languoto_fasado_plotis 	= 5 	#
	attr balkonu_sekcijos_plotis 	= 4.2 	#
	attr azuro_sekcijos_plotis		= 4.50 	#
	attr H_iejimo_fasado			= 3.5	#
	attr H_azuro_bloko				= 2.8	#
	attr H_azuro_perdangos			= 0.05	#
	attr Gylis_laiptines			= 1.2	#
	attr Gylis_azuro_sekcijos		= 0.25	#
	attr langai_galiniuose_fasaduose = 1	#
	attr skaidymo_asis				= "y"
	
	attr langu_fasado_plotis 		= 4.5
	attr ventiliacijos_bokstelio_aukstis = 0.7
	//}

@Group ("Spalvos") //{
	@Color
	attr sienos_spalva 				= "#bdbf9c"
	attr laiptines_sienos_spalva 	= "#babaad"
	attr laiptines_duru_spalva 		= "#d6c9a2"
	attr perdangos_spalva 			= "#c4c4c2"
	attr pamato_spalva 				= "#858585"
	attr stiklo_spalva 				= "#c2d1cb"
	attr stogo_spalva				= "#616468"
	attr parapeto_spalva 			= "#939393"
	attr balkono_sieneles_spalva	= "#f6f990"
	//}
	
@Group ("Lango_remo_spalvos") //{
	attr lango_remo_spalva_nr1		= "#efefef"
	attr lango_remo_spalva_nr2		= "#a98c67"
	attr lango_remo_spalva_nr3		= "#7a4604"
	attr lango_remo_spalva_nr4		= "#c3c07c"
//}
	
@Group ("Lango_stiklo_spalvos")
	attr lango_stiklo_spalva_nr1	= "#00c5ff"
	attr lango_stiklo_spalva_nr2	= "#0078ff"
	attr lango_stiklo_spalva_nr3	= "#004087"
	attr lango_stiklo_spalva_nr4	= "#6e6fe3"

#---------------------------------#

#---------- OTHER RULES -----------#
Geltona --> 
	color (yellow)
	//}
Raudona --> 
	color (red)
	//}
Melyna --> 
	color (blue)
	//}
	
Zalia --> 
	color (green)
	//}

Violetine -->
	color (violet)
	//}
	
Pilka1 -->
	color ("#a9aa8f")
	
Pilka2 -->
	color ("#5b5b56")
	
Pilka3 -->
	color ("#cececa")
//______________________________________//
@StartRule
A --> 
	extrude (world.up, H_pastato)
		alignScopeToAxes (y)
			comp (f)	{object.top						: Stogo_kurimas
						|object.bottom					: NIL
						|Galinio_fasado_plokstumos_nr	: Galinio_fasado_horizontalus_skaidymas
						|Priekinio_fasado_plokstumos_nr	: Priekinio_fasado_kurimas
						|all							: Soninio_fasado_kurimas
						
						}

#--------------------------------------------------------#
Priekinio_fasado_kurimas -->
	alignScopeToAxes (y)
		split (x) 	{
					{languoto_fasado_plotis		: Fasado_kurimas
					|~balkonu_sekcijos_plotis	: Balkonu_sekcijos_kurimas
					|azuro_sekcijos_plotis		: Azuro_sekcijos_kurimas
					|~balkonu_sekcijos_plotis	: Balkonu_sekcijos_kurimas
					|languoto_fasado_plotis		: Fasado_kurimas
					}*
					}
#--------------------------------------------------------#

#-----------------------Balkonai--------------------------#
Balkonu_sekcijos_kurimas --> Balkonu_sekcijos_horizontalus_skaidymas
	//extrude (0.9)
		//comp (f) 	{ top		: Balkonu_sekcijos_horizontalus_skaidymas
					//| bottom	: NIL
					//| front		: NIL
	      			//| back		: NIL
	     			//| side		: NIL
					//}
							
				
#--------------------------------------------------------#

#--------------------Balkonu sekicija----------------------#

Balkonu_sekcijos_horizontalus_skaidymas -->
	split (y) 	{ H_pamato				: Priekinio_fasado_pamatas
				|H_perdangos			: Perdanga
				|~H_pirmo_auksto		: Balkonu_sekcijos_priekis
				|
				{~H_vidurinio_auksto	: Balkonu_sekcijos_priekis
				}*
				|H_pastoges				: Pastoge
				}

Balkonu_sekcijos_priekis -->
	comp (f) 	{ top		: NIL
				| bottom	: NIL
				| front		: Balkono_priekio_kurimas
	      		| back		: NIL
	     		| side		: NIL
				}
					
	
Balkono_priekio_kurimas -->
	r (scopeCenter,0,0,0)
	split(y) 	{~H_polangio 	: Fasadinio_balkono_kurimas
             	|~H_lango		: Fasadinio_balkono_lango_zona
             	|~H_virslangio	: Virslangio_zona
             	|H_perdangos	: Fasadinio_balkono_perdanga
             	}

Fasadinio_balkono_kurimas -->
	extrude (Gylis_laiptines-Gylis_azuro_sekcijos)
		comp (f) 	{ top		: Fasadinio_balkono_sieneles_kurimas
					| bottom	: Sienos_spalvinimas
					| front		: NIL
	      			| back		: NIL
	     			| left		: Fasadinio_balkono_sieneles_kurimas
	     			| right		: Fasadinio_balkono_sieneles_kurimas
					}

Fasadinio_balkono_sieneles_kurimas -->
	extrude (0.05)
		Geltona
		
Fasadinio_balkono_lango_zona -->
	split(x){
			{~1 				: Sienos_spalvinimas 
            |Plotis_lango		: Lango_nisa
            |~1					: Sienos_spalvinimas
            }*
            }

Fasadinio_balkono_perdanga -->
	extrude (Gylis_laiptines-Gylis_azuro_sekcijos)
		Perdanga

#--------------------Azuro sekicija----------------------#
Azuro_sekcijos_kurimas --> 
	extrude (Gylis_laiptines)
		alignScopeToAxes (x)
			split (z) 	{H_perdangos		: Raudona
						|
						{H_perdangos		: Perdanga
						|~H_azuro_bloko		: Azuro_bloko_kurimas
						}*
						|H_iejimo_fasado	: Iejimo_fasado_kurimas
						}

Azuro_bloko_kurimas --> 
	comp (f)	{top	: NIL
      			|bottom	: Sienos_spalvinimas
      			|left	: Sienos_spalvinimas
      			|right	: Azuro_bloko_iterpimas
      			|all	: NIL
   				}						

Azuro_bloko_iterpimas -->
	alignScopeToAxes (z)
		i (AzuroBlokas)
			s (Gylis_azuro_sekcijos,'1,'1.2)
				t (-Gylis_azuro_sekcijos,0,-0.5)
					Pilka3
					
#--------------------------------------------------------#
Iejimo_fasado_kurimas -->
	comp (f)	{top	: Pilka3
      			|bottom	: Pilka3
      			|left	: Pilka3
      			|right	: B
      			|all	: Pilka3
   				}

B -->
	offset(-0.1) 
		comp(f)	{inside: NIL 
				| border: Pilka3
				}

#______________Side facade_____________#
Soninio_fasado_kurimas --> 
 case langai_galiniuose_fasaduose == 1	: Languoto_soninio_fasado_horizontalus_skaidymas
 else									: Belangio_soninio_fasado_horizontalus_skaidymas

Languoto_soninio_fasado_horizontalus_skaidymas -->
	alignScopeToAxes (y)
		split (x) 	{
					{~H_vidurinio_auksto*1.2		: Soninio_fasado_vertikalus_skaidymas
					|H_perdangos					: Soninio_fasado_siuliu_vertikalus_skaidymas
					}*
					|H_vidurinio_auksto*1.2			: Languoto_soninio_fasado_vertikalus_skaidymas
					|H_perdangos					: Soninio_fasado_siuliu_vertikalus_skaidymas
					|~H_vidurinio_auksto*1.2		: Soninio_fasado_vertikalus_skaidymas
					}

Belangio_soninio_fasado_horizontalus_skaidymas -->
	alignScopeToAxes (y)
		split (x) 	{
					{~H_vidurinio_auksto*1.2		: Soninio_fasado_vertikalus_skaidymas
					|H_perdangos					: Soninio_fasado_siuliu_vertikalus_skaidymas
					}*
					|H_vidurinio_auksto*1.2			: Soninio_fasado_vertikalus_skaidymas
					|H_perdangos					: Soninio_fasado_siuliu_vertikalus_skaidymas
					|~H_vidurinio_auksto*1.2		: Soninio_fasado_vertikalus_skaidymas
					}
					

Languoto_soninio_fasado_vertikalus_skaidymas -->					
	split (y) 	{H_pamato 							: Soninio_fasado_pamatas
				|~H_pirmo_auksto - H_perdangos		: Priekinio_kambario_fasadas
				|H_perdangos						: Perdanga
				|
				{~H_vidurinio_auksto - H_perdangos 	: Priekinio_kambario_fasadas
				|H_perdangos						: Perdanga
				}*
				|H_pastoges							: Pastoge
				}

Soninio_fasado_vertikalus_skaidymas -->					
	split (y) 	{H_pamato 							: Soninio_fasado_pamatas
				|~H_pirmo_auksto - H_perdangos		: Namo_galine_siena
				|H_perdangos						: Perdanga
				|
				{~H_vidurinio_auksto - H_perdangos 	: Namo_galine_siena
				|H_perdangos						: Perdanga
				}*
				|H_pastoges							: Pastoge
				}
				//}

Soninio_fasado_siuliu_vertikalus_skaidymas 	-->
	split (y) 	{H_pamato 							: Pamato_spalvinimas
				|~H_pirmo_auksto - H_perdangos		: Perdanga
				|H_perdangos						: Perdanga
				|
				{~H_vidurinio_auksto - H_perdangos 	: Perdanga
				|H_perdangos						: Perdanga
				}*
				|H_pastoges							: Pastoge
				}
			
				
Namo_galine_siena -->
	Sienos_spalvinimas
	//}

Soninio_fasado_pamatas -->
	split (x) 	{~laiptines_fasado_plotis	: Pamato_spalvinimas
				|~languoto_fasado_plotis	: Vieno_lygio_Rusio_su_langu_fasadas
				|~laiptines_fasado_plotis	: Pamato_spalvinimas
				}
				//}

#______________Roof_____________#
Stogo_kurimas --> 
	offset(plotis_parapeto * -1)
		comp (f) 	{inside	: Stogo_plokstuma
					|border : Parapetas
					}
					//}

Stogo_plokstuma --> 
	extrude (world.up, 1)
		comp (f)	{top	: Erdve_objektams_ant_stogo	
      				|bottom	: Stogo_spalvinimas	
      				|side	: NIL 
   					}
					//}

Erdve_objektams_ant_stogo -->
	offset(-0.1)
		comp(f) {inside	: NIL
				|border : NIL
				}
				//}

Stogo_spalvinimas --> 
	reverseNormals
		color (stogo_spalva)
		//}

Objektu_ant_stogo_kurimas -->
	case scope.sx>scope.sy 	: Objektu_ant_stogo_kurimas_x
	else					: Objektu_ant_stogo_kurimas_y
   				
Objektu_ant_stogo_kurimas_x -->	
	split (x) 		{
					{languoto_fasado_plotis		: NIL
					|~balkonu_sekcijos_plotis	: NIL
					|azuro_sekcijos_plotis		: Liftines_pagrindo_kurimas
					|~balkonu_sekcijos_plotis	: NIL
					|languoto_fasado_plotis		: NIL
					}*
					}

Objektu_ant_stogo_kurimas_y -->	
	split (y) 		{
					{languoto_fasado_plotis		: NIL
					|~balkonu_sekcijos_plotis	: NIL
					|azuro_sekcijos_plotis		: Liftines_pagrindo_kurimas
					|~balkonu_sekcijos_plotis	: NIL
					|languoto_fasado_plotis		: NIL
					}*
					}

Liftines_pagrindo_kurimas -->
	extrude (2.5)
	alignScopeToAxes (y)
		t (0,(H_parapeto*-2)-1,0)
			s('0.5,'1,'1)
				Liftines_turio_skaidymas

Liftines_turio_skaidymas -->
	comp(f)			{ top	: Liftines_stogo_kurimas	
      				|bottom	: NIL 	
      				|all	: Sienos_spalvinimas  
   					}

Liftines_stogo_kurimas -->
	offset(plotis_parapeto * -1)
		comp (f) 	{inside	: Liftines_stogo_plokstuma
					|border : Parapetas
					}
					//}

Liftines_stogo_plokstuma --> 
	extrude (world.up, 1)
		comp (f)	{top	: NIL	
      				|bottom	: Stogo_spalvinimas	
      				|side	: NIL 
					}

Parapetas -->
	extrude (H_parapeto)
		color (parapeto_spalva)
			comp(f)	{ top	: Parapeto_spalvinimas	
      				|bottom	: NIL 	
      				|all	: Parapeto_spalvinimas  
   					}
					//}
   					
Parapeto_spalvinimas -->
	color (parapeto_spalva)
	//}
	

#___________________________________#

Fasado_kurimas -->
	Priekinio_fasado_horizontalus_skaidymas

Priekinio_fasado_horizontalus_skaidymas --> 
	split (y) 	{ H_pamato				: Priekinio_fasado_pamatas
				|H_perdangos			: Perdanga
				|~H_pirmo_auksto		: Priekinio_fasado_pirmas_aukstas
				|
				{~H_vidurinio_auksto	: Priekinio_fasado_vidurinis_aukstas
				}*
				|H_pastoges				: Pastoge
				}
				

	//}
Priekinio_fasado_pirmas_aukstas --> 
	Vertikalus_priekinio_fasado_pirmo_auksto_skaidymas
	//}

Priekinio_fasado_vidurinis_aukstas --> 
	Vertikalus_priekinio_fasado_vidurinio_auksto_skaidymas
	//}

Vertikalus_priekinio_fasado_pirmo_auksto_skaidymas -->
	alignScopeToAxes (y)
		split (x) 	{
					{~languoto_fasado_plotis	: Priekinio_kambario_fasadas
					|~laiptines_fasado_plotis	: Zalia
					}*
					|~languoto_fasado_plotis 	: Priekinio_kambario_fasadas
					}
					//}

Vertikalus_priekinio_fasado_vidurinio_auksto_skaidymas -->
	alignScopeToAxes (y)
		split (x) 	{
					{~languoto_fasado_plotis	: Priekinio_kambario_fasadas
					|~laiptines_fasado_plotis	: NIL
					}*
					|~languoto_fasado_plotis 	: Priekinio_kambario_fasadas
					}
					//}
					
Priekinio_fasado_pamatas --> 
	split (x) 	{
				{~languoto_fasado_plotis	: Vieno_lygio_Rusio_su_langu_fasadas
				|~laiptines_fasado_plotis	: NIL
				}*
				|~languoto_fasado_plotis 	: Vieno_lygio_Rusio_su_langu_fasadas
				}
				//}
				
#______________Front facade Room facade_____________#
Priekinio_kambario_fasadas --> 
	split(y) 	{~H_polangio 	: Polangio_zona
             	|~H_lango		: Lango_zona
             	|~H_virslangio	: Virslangio_zona
             	|H_perdangos	: Perdanga
             	}
				//}
#______________Window_____________#           
Polangio_zona -->
	Sienos_spalvinimas
	//}
	
Lango_zona -->
	split(x){
			{~0.5 				: Sienos_spalvinimas 
            |Plotis_lango		: Lango_nisa
            |~0.5				: Sienos_spalvinimas
            }*
            }
			//}

Virslangio_zona -->
	Sienos_spalvinimas
	//}

Lango_nisa -->
	extrude (0.25)
		t (0, -0.25, 0)
			comp (f) 	{top	: NIL
						|bottom	: reverseNormals 
									Lango_remo_kurimas
						|all	: reverseNormals 
									Sienos_spalvinimas
						}
						//}

Lango_remo_kurimas -->
	offset(-0.07)
		comp (f) 	{border = Lango_remas
					|inside	: Stiklas
					}
					//}

Lango_remas -->
	extrude (0.05)
		Lango_remo_spalvinimas
		//}		

Lango_remo_spalvinimas -->
	30% 	:	color (lango_remo_spalva_nr1)
	30% 	:	color (lango_remo_spalva_nr2)
	30% 	:	color (lango_remo_spalva_nr3)
	else	:	color (lango_remo_spalva_nr4)
	//}

Stiklas -->
	30% 	:	color (lango_stiklo_spalva_nr1)
	30% 	:	color (lango_stiklo_spalva_nr2)
	30% 	:	color (lango_stiklo_spalva_nr3)
	else	:	color (lango_stiklo_spalva_nr4)
	//}
#___________________________________#


#______________Basement_____________#
Vieno_lygio_Rusio_su_langu_fasadas -->
	split(y) 	{~1 	: Rusio_polangio_zona
             	|~1.2 	: Rusio_lango_zona
             	|~0.5	: Rusio_virslangio_zona
             	}
				//}
Rusio_po_balkonu_sienele --> 
	extrude (1)
		Pamato_spalvinimas
		//}

Rusio_polangio_zona -->
	color (pamato_spalva)
	//}

Rusio_lango_zona -->
	split(x){
			{~1 			: Pamato_spalvinimas 
            |Plotis_lango 	: Rusio_langas
            |~1				: Pamato_spalvinimas 
            }*
            }
			//}

Rusio_langas -->
	extrude (0.15)
		t (0, -0.15, 0)
			comp (f) 	{top	: NIL
						|bottom	: reverseNormals 
									Rusio_lango_medziaga
						|all	: reverseNormals 
									Pamato_spalvinimas
						}
						//}

Rusio_lango_medziaga -->
	60%		: Rusio_langas_groteles
	30%		: Rusio_langas_stiklas
	else	: Rusio_langas_uzmurytas
	//}

Rusio_langas_groteles -->
	color ("#90C583")
	//}
	
Rusio_langas_uzmurytas -->
	color ("#c5a82f")
	//}

Rusio_langas_stiklas -->
	color ("#58a6bd")
	//}

Rusio_virslangio_zona -->
	Pamato_spalvinimas
	//}				
				
#--------------------------------------------------#

#______________Back facade_____________#
Galinio_fasado_horizontalus_skaidymas --> 
	split (y) 	{H_pamato				: Galinio_fasado_pamatas
				|~H_pirmo_auksto		: Galinio_fasado_pirmas_aukstas
				|
				{~H_vidurinio_auksto	: Galinio_fasado_vidurinis_aukstas
				}*
				|H_pastoges				: Pastoge
				}
				//}

Galinio_fasado_pirmas_aukstas --> 
	Vertikalus_galinio_fasado_skaidymas
	//}

Galinio_fasado_vidurinis_aukstas --> 
	Vertikalus_galinio_fasado_skaidymas
	//}

Vertikalus_galinio_fasado_skaidymas -->
	Skaidymas_sekcijomis
	
Skaidymas_sekcijomis -->
	split (x) 	{~(70/3) 	: Galinio_fasado_sekcija
				}*

Galinio_fasado_sekcija -->	
	split (x) 	{~1 	: Balkonas
				|~1 	: Balkonas
				|~2 	: Galinio_kambario_fasadas
				|~1 	: Balkonas
				|~1 	: Balkonas
				}


Galinio_fasado_pamatas --> 
	split (x) 	{
				{~balkono_plotis		: Itraukto_Rusio_su_langu_fasadas
				|~balkono_plotis		: Itraukto_Rusio_su_langu_fasadas
				|~langu_fasado_plotis/2	: Vieno_lygio_Rusio_su_langu_fasadas
				|~langu_fasado_plotis/2	: Vieno_lygio_Rusio_su_langu_fasadas
				|~balkono_plotis		: Itraukto_Rusio_su_langu_fasadas
				|~balkono_plotis		: Itraukto_Rusio_su_langu_fasadas
				}*
				}
				//}
#___________________________________#

Itraukto_Rusio_su_langu_fasadas -->
	extrude (1.0)
		t (0, -1.0, 0)
			alignScopeToAxes (y)
				comp (f) 	{ top	: reverseNormals 
										Perdanga
							| bottom	: NIL
							| front	: NIL 
      						| back	: reverseNormals 
      									Rusio_po_balkonu_su_langu_fasadas
      						| left	: reverseNormals
      									Pamato_spalvinimas
      						| right	: reverseNormals
      									Pamato_spalvinimas
							}
							//}

Rusio_po_balkonu_su_langu_fasadas -->
	split(x){
			{~0.1	: Rusio_po_balkonu_sienele
			|~2		: Vieno_lygio_Rusio_su_langu_fasadas
            }*
            }
			//}

#______________Back facade Room facade_____________#
Galinio_kambario_fasadas --> 
	split(y) 	{~H_polangio 	: Polangio_zona
             	|~H_lango		: Lango_zona
             	|~H_virslangio	: Virslangio_zona
             	|H_perdangos	: Perdanga
             	}
				//}
#___________________________________#

#______________Balcony_____________# 
Balkonas --> 
	split(y) 	{~2.2 			: Balkono_fasadas
             	|H_perdangos	: Perdanga
             	}
				//}

Balkono_fasadas --> 	
	split(x){
			{~2		: Balkono_kurimas
            |~0.1	: Sienos_spalvinimas 
            }
            }
			//}
            
Balkono_kurimas -->
	extrude (1.0)
		t (0, -1.0, 0)
			alignScopeToAxes (y)
				comp (f) 	{ top	: reverseNormals 
										Perdanga
							| bottom	: reverseNormals 
										Perdanga
							|front	: Balkono_isorinis_fasadas 
      						|back	: reverseNormals 
      									Balkono_kambario_fasadas
      						|left	: reverseNormals
      									Sienos_spalvinimas
      						|right	: reverseNormals
      									Sienos_spalvinimas
							}
							//}
						
Balkono_isorinis_fasadas -->
	split(y) 	{~H_polangio 				: Balkono_sienele
             	|~H_virslangio + H_lango 	: Ar_balkonas_istiklintas
             	}
				//}
             			
Balkono_sienele --> 
	s ('0.99, '1, '1)
		extrude (0.10)
	 		t (0, -0.1, 0)
	 		alignScopeToAxes (y)
	 			comp(f)	{top	: Balkono_tureklo_spalvinimas	
      					|all	: Balkono_sieneles_spalvinimas 	
   						}
						//}

Balkono_sieneles_spalvinimas -->				
	color (balkono_sieneles_spalva)
	//}

Balkono_tureklo_spalvinimas -->				
	color ("#67767a")
	//}

Ar_balkonas_istiklintas -->
	10%  : Balkono_langas
	else : NIL
	//}

Balkono_langas -->
	t (0, 0, -0.08)
		Atsitiktinis_balkono_langas
		//}
		
Atsitiktinis_balkono_langas -->
	60%		: Triju_daliu_balkono_langas
	30%		: Sesiu_daliu_balkono_langas
	8%		: Keturiu_daliu_balkono_langas
	else	: Vienos_dalies_balkono_langas
	//}

Vienos_dalies_balkono_langas --> 
	Lango_remo_kurimas
	//}

Triju_daliu_balkono_langas -->
	split (x)	{~0.33 	: Lango_remo_kurimas
				|~0.33	: Lango_remo_kurimas
				|~0.33	: Lango_remo_kurimas
				}
				//}

Keturiu_daliu_balkono_langas -->
	split (x)	{~0.25 	: Lango_remo_kurimas
				|~0.25	: Lango_remo_kurimas
				|~0.25	: Lango_remo_kurimas
				|~0.25	: Lango_remo_kurimas
				}
				//}

Sesiu_daliu_balkono_langas -->
	split (x)	{~0.25 	: Balkono_lango_skaidymas_horizontaliai
				|~0.25	: Balkono_lango_skaidymas_horizontaliai
				|~0.25	: Balkono_lango_skaidymas_horizontaliai
				}
				//}

Balkono_lango_skaidymas_horizontaliai -->
	split (y) 	{~0.5	: Lango_remo_kurimas
				|~0.2	: Lango_remo_kurimas		
				}
				//}				
				
					
Balkono_kambario_fasadas -->
		split(y) 	{~H_polangio 		: Polangio_zona
             		|~H_lango			: Lango_zona
             		|~H_virslangio		: Virslangio_zona
             		}
					//}
#___________________________________#

				
#-------------------------------------------------#
Perdanga -->
	color (perdangos_spalva)
	//}

Pastoge --> 
	Sienos_spalvinimas
	//}

Sienos_spalvinimas -->
	color (sienos_spalva)
	//}
Pamato_spalvinimas -->
	color (pamato_spalva)				